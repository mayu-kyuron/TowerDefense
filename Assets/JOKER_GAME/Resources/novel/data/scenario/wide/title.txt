
*title
;タイトル画面
@call file=wide/libs/utils

[if exp="{bgm.number}==null"]
	[calc exp="bgm.number=0.6"]
	[calc exp="se.number=0.6"]
[elsif exp="{bgm.number}==0"]
	[calc exp="bgm.number=0.6"]
	[calc exp="se.number=0.6"]
[else]
	[if exp="{is.fromEnding}==null"]
		[calc exp="bgm.number={bgm.number}*0.2"]
		[calc exp="se.number={se.number}*0.2"]
	[endif]
[endif]

[flag exp="is.fromTitle=true"]
[flag exp="is.fromEnding=null"]

[playbgm storage="title" wait=false vol={bgm.number}]

[bg_new name="background" layer="background" storage="title"]
[bg_show name="background"]

;開始ボタンとロードボタンを実装
@image_new name="button_start" tag="button" x=-3.5 y=-3.2  storage="title_start" scale=1.2
@image_new name="button_load" tag="button" x=3.5 y=-3.2  storage="title_load" scale=1.2

;autoという変数にオートセーブのデータを代入
@get_autosave var=auto

@show tag=button
@anim name=button_start storage="onmouse"

*no

[evt name="button_start" act="click" target="*click_start" ]
[evt name="button_load" act="click" target="*click_load" ]

[evt_resume]

[s]

*click_start
[evt_stop]
[playse storage="start"]
@anim name="button_start" scale=1.5 wait=false

;オートセーブされたデータが存在するなら
[if exp="{auto.date}!=''"]

	[wait time = 1]

	[showmessage]
	@anim name="button_start" scale=1.2 wait=false

	[wait time = 1]

	はじめからを選択すると、今までのデータが消去されます。[r][l]
	はじめからプレイしますか？

	[wait time =1]

	[select_new text="はい" target=*select_ok ]
	[select_new text="いいえ" target=*select_no ]

	[evt_resume tag="select_button"]

	[select_show sound=click ]

	[s]

	*select_ok

	[evt_stop]
	[cm]
	[hidemessage]

	@jump target=*ok

	*select_no

	[cm]
	[hidemessage]

	[evt_resume]

	[s]

[endif]

*ok

@bg_hide name="background" time=1.5

[stopbgm storage="title" wait=false]
[wait time=2]

;判断用変数の初期化
[calc exp="stage.number=null"]
[calc exp="bgm.number=null"]

@jump file=wide/material scene=new

*click_load
[evt_stop]
[playse storage="start"]
@anim name="button_load" scale=1.5 wait=true

[if exp="{auto.date}==''"]

	[showmessage]
	@anim name="button_load" scale=1.2 wait=false

	[wait time = 1]

	データがありません。[p]
	[cm]

	[hidemessage]

	@jump target = *no

[endif]

[load name = "autosave"]